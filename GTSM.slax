version 1.1;
/* created 03APR2018 akg
this commit script implments GTMS (RFC 5082) for select BGP peers
peers are idenitified by the statement 'apply-macro GTSM' in the bgp group stanza
It works by placing any neighbor addresses in the stanza into a family-specific prefix list
which is used in a *pre-existing* filter applied to the loopback.

See README.md for example filter term syntax.

There is an existing script in the Juniper Scriptorium:
https://github.com/Juniper/junoscriptorium/blob/master/library/juniper/commit/protocols/ttl-security/ttl
-security.slax

Key differences:
This scripts supports both IPv4 and IPv6 by using family specific prefix-lists that are used in firewall filters.
The original script puts v4 addresses directly into a firewall filter term.
The original script detects the state of lo0 filtering and will add a new filter or modify a filter-list as appropriate
This script assumes that a lo0 filter exists with a term to handle the TTL filtering
This script makes permenant changes; the other, transient changes
*/


ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";

import "../import/junos.xsl";



match configuration {
    var $configroot = .;
    for-each (protocols/bgp/group) {
        var $group = ./name;
        if (apply-macro/name == "GTSM") {
            call jcs:emit-change($dot=$configroot) {
                with $content = {
                    <protocols> {
                        <bgp> {
                            <group> {
                                <name> $group;
                                    <multihop> {
                                        <ttl> 255;
                                }
                            }
                        }
                    }
                }
            }
            for-each (neighbor) {
                if (contains(., '.')) {
                    call jcs:emit-change($dot=$configroot) {
                        with $content = {
                        <policy-options> {
                            <prefix-list> {
                                <name>"pf_GTSM_neighbors_v4";
                                    <prefix-list-item> {
                                        <name> .;
                                    }
                                }
                            }
                        }
                    }
                }
                if (contains(., ':')) {
                    call jcs:emit-change($dot=$configroot) {
                    with $content = {
                        <policy-options> {
                            <prefix-list> {
                                <name>"pf_GTSM_neighbors_v6";
                                    <prefix-list-item> {
                                        <name> .;
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
